\subsection{Non-ground reification and transformation}


\begin{center}
\begin{minipage}{\linewidth}
  \begin{lstlisting}[]
operator_arity((initial;final),0).
operator_arity(
  (prev;weak_prev;next;weak_next;neg;always_after;always_before;
   eventually_after;eventually_before),1).
operator_arity((until;since;release;trigger;and;or),2).

arg((weak_prev;weak_next),0,unsafe).
arg((until;since;release;trigger),0,unsafe).
arg(or,(0;1),unsafe).
arg(Opname,Arg,safe)
  :- operator_arity(Opname,Arity), Arg=0..Arity-1, 
     not arg(Opname,Arg,unsafe).

func_arity(F,A) :- function(F,_,terms(TS)), 
                   A = #count{ P: terms(TS,P,_) }.

operator(function(F))
  :- function(F,Name,terms(T)), operator_arity(Name,Arity), 
     func_arity(F,Arity).

operand(function(F),function(F'),Safety)
  :- operator(function(F)), function(F,Name,terms(T)), arg(Name,P,Safety),
		 terms(T,P,function(F')).

root_operator(Func)
  :- operator(Func), symbolic_atom(_,Func).

root2desc_operand(Root,Operand,Safety)
  :- root_operator(Root), operand(Root,Operand,Safety).
root2desc_operand(Root,Operand',Safety)
  :- root2desc_operand(Root,Operand,Safety), 
     operand(Operand,Operand',safe).
root2desc_operand(Root,Operand',unsafe)
  :- root2desc_operand(Root,Operand,Safety), 
     operand(Operand,Operand',unsafe).
root2desc_leaf_operand(Root,Operand,Safety)
  :- root2desc_operand(Root,Operand,Safety), 
     not operator(Operand).

\end{lstlisting}
\captionof{lstlisting}{generate-external.lp - part 1}
\end{minipage}
\end{center}


\begin{center}
\begin{minipage}{\linewidth}
  \begin{lstlisting}[]
atom_sign(A,Sign)
  :- literal(_,Sign,symbolic_atom(A)), symbolic_atom(A,_).
atom_sign(A,Sign)
  :- body_literal(_,Sign,symbolic_atom(A)), symbolic_atom(A,_).

head_root_op(Func)
  :-  literal(L,"pos",symbolic_atom(S)), symbolic_atom(S,Func),
      root_operator(Func), not literals(_,_,literal(L)).
body_root_op(Func) 
  :- not head_root_op(Func), root_operator(Func).

safe_atomic_subformula(Func,Func) 
  :- symbolic_atom(A,Func), not root_operator(Func), 
     atom_sign(A,"pos").
safe_atomic_subformula(Func,Func')
  :- symbolic_atom(A,Func), root_operator(Func), 
     atom_sign(A,"pos"), 
     root2desc_leaf_operand(Func,Func',safe).

stm2desc((statements(STM),Pos),Child) 
  :- statements(STM,Pos,Child).
stm2desc(Statement,Child') 
  :- stm2desc(Statement,Child), not root_operator(Child), 
     child(Child,Child').

root2extern_cond(Func,Func'')
  :- stm2desc(Statement,Func), root_operator(Func), 
     stm2desc(Statement,body_literal(BL)), 
     body_literal(BL,"pos",symbolic_atom(S)), 
     symbolic_atom(S,Func'), 
     safe_atomic_subformula(Func',Func'').

ast(add(program(externals,"base",constants(externals),
                statements(externals)))).

ast(add(statements(externals,pos(Func,Operand),
                   external(Operand));
        external(Operand,symbolic_atom(Operand),
                 body_literals(Func),false);
        symbolic_atom(Operand,Operand)))
  :- head_root_op(Func), root2desc_leaf_operand(Func,Operand,_).

ast(add(statements(externals,pos(Func),external(Func));
        external(Func,symbolic_atom(Func),
                 body_literals(Func),false);
	symbolic_atom(Func,Func)))
  :- body_root_op(Func).

desc_root_op(Func,Child) 
  :- root_operator(Func), child(Func,Child).
desc_root_op(Func,Child')
  :- desc_root_op(Func,Child), child(Child,Child').
has_var(Func) :- desc_root_op(Func,variable(V)).

ast(add(body_literals(Func,pos(Func'),body_literal(Func'));
        body_literal(Func',"pos",symbolic_atom(Func'));
        symbolic_atom(Func',Func')))
  :- root_operator(Func), root2extern_cond(Func,Func'), 
		 #false : not has_var(Func), body_root_op(Func).
\end{lstlisting}
\captionof{lstlisting}{generate-external.lp - part 2}
\end{minipage}
\end{center}
