\subsection{Metaprogramming Implementation}

Metaprogramming is a generic technique in programming languages, where
an input program is translated, or \emph{reified} into some primitive
datatype of the language. This reified representation then serve as
input to a meta program, which in turn may reinterpret the semantics
of the reified program. The complexity of this translation varies
based on the complexity of the language's syntax. Lisp may serve as a
gold standard here, where reification is simply done by quotation. 

The ASP grounder gringo offers native support for meta-programming by
first grounding the input program, and then serializing a
representation of the ground program as a set of facts
\cite{karoscwa21a}. This set of facts can then be fed as input to a
\emph{meta-encoding}, which can reinterpret the semantics of the
ground language constructs. The transformation $\tau(\Gamma)$
described in the previous section will be implemented by such a
meta-encoding. We will not detail the reification format in this
thesis. For a description of the format, and some examples of simpler
meta-encodings, we refer the reader to \cite{karoscwa21a}.

\subsubsection{Input language}

Syntactically, the input language for meta-telingo is disjunctive
logic programs in the language of the ASP grounder gringo
\cite{PotasscoUserGuide19} \cite{gescth07a}. All rules are implicitly
considered to be in the scope of an $\alwaysF$ operator. Temporal
operators are represented via prefix notation as a symbol. Note that
when we say symbol, we consider symbolic functions as well as symbolic
atoms in the gringo language to be symbols. For a symbol to be
considered a temporal operator two conditions must be met. First, the
names and arities of such symbols must match the written name of the
given operator, and their arity; the written names of the connectives
$\vee, \wedge, \neg$ are considered to be \emph{or, and, neg},
respectively. For example \verb|since(a,b)| corresponds to
$a \since b$. Second, the symbol must not occur nested within a symbol
that is not an operator, i.e. in \verb|dog(next(spotty))|, the
\verb|next/1| is not considered a temporal operator, as \verb|dog/1|
is not an operator.

We call this representation of a temporal program it's \emph{gringo
  representation}.

\begin{example}\label{exam:shoot-ground-symbolic}
  The gringo representation of the temporal program in Example
  \ref{exam:shoot-ground-program} is the following gringo program:
\begin{center}
    \begin{lstlisting}[numbers=none]
shoot :- initial.
next(not_load) :- initial.
next(next(shoot)) :- initial
and(eventually_after(fail),forgetful) 
  :- shoot, prev(since(not_load,shoot)).
    \end{lstlisting}
\end{center}
\end{example}

\begin{center}
\begin{minipage}{\linewidth}
\begin{lstlisting}[language=clingo, label={lst:meta-telingo-one}]
time(0..lambda-1).

conjunction(B,K) :- literal_tuple(B), time(K),
        hold(L,K) : literal_tuple(B, L), L > 0;
    not hold(L,K) : literal_tuple(B,-L), L > 0.
body(normal(B),K) :- rule(_,normal(B)), conjunction(B,K), 
                     time(K).

hold(A,K) : atom_tuple(H,A) :- rule(disjunction(H),B), 
                               body(B,K), time(K).


true(O,K) :- hold(L,K), output(O,B), literal_tuple(B,L).
hold(L,K) :- true(O,K), output(O,B), literal_tuple(B,L).
true(O,K) :- time(K), output(O,B), not literal_tuple(B,_).

subformula(O) :- output(O,_).
subformula((F;G)) :- subformula((and(F,G);or(F,G);until(F,G);since(F,G))).
subformula(F)	 :- subformula((neg(F);next(F);prev(F))).

true(true,K) :- subformula(true), time(K).
:- subformula(false), time(K), true(false,K).

true(and(F,G),K) :- subformula(and(F,G)), true(F,K), true(G,K).
true((F;G),K) :- subformula(and(F,G)), true(and(F,G),K).
true(or(F,G),K) :- subformula(or(F,G)), true((F;G),K).
true(F,K);true(G,K) :- subformula(or(F,G)), true(or(F,G),K).

true(neg(F),K) :- subformula(neg(F)), time(K), not true(F,K).
not true(F,K) :- subformula(neg(F)), true(neg(F),K).

true(initial,0) :- subformula(initial).
:- true(initial,K), subformula(initial), K!=0.
true(final,lambda-1) :- subformula(final).
:- true(final,K), subformula(final), K!=lambda-1.

true(F,K+1): time(K+1) :- subformula(next(F)), true(next(F),K).
true(next(F),K) :- true(F,K+1), subformula(next(F)), time(K).

true(F,K-1): time(K-1) :- subformula(prev(F)), true(prev(F),K).
true(prev(F),K) :- true(F,K-1), subformula(prev(F)), time(K).

true(until(L,R,J),K): K<=J, time(J)
  :- subformula(until(L,R)), true(until(L,R),K), time(K).
true(until(L,R),K)
  :- subformula(until(L,R)), true(until(L,R,_),K).
true(L,I) :- true(until(L,R,J),K), I=K..J-1.
true(R,J) :- true(until(L,R,J),K).
true(until(L,R,J),K)
  :- subformula(until(L,R)), time(K), time(J), K<=J, true(R,J), 
     true(L,I) : I=K..J-1.

true(since(L,R,J),K): J<=K, time(J)
  :- subformula(since(L,R)), true(since(L,R),K), time(K).
true(since(L,R),K)
  :- subformula(since(L,R)), true(since(L,R,_),K).
true(L,I) :- true(since(L,R,J),K), I=J+1..K.
true(R,J) :- true(since(L,R,J),K).
true(since(L,R,J),K)
  :- subformula(since(L,R)), time(K), time(J), J<=K, true(R,J), 
     true(L,I) : I=J+1..K.
\end{lstlisting}
\captionof{lstlisting}{meta-telingo.lp - part 1}
\end{minipage}
\end{center}

\begin{center}
\begin{minipage}{\linewidth}
  \begin{lstlisting}[language=clingo, label={lst:meta-telingo2}]
derive(wnext(F),or(next(F),final)) :- subformula(wnext(F)).
derive(wprev(F),or(prev(F),initial)) :- subformula(wprev(F)).
derive(eventually_after(F),until(true,F)) 
  :- subformula(eventually_after(F)).
derive(eventually_before(F),since(true,F)) 
  :- subformula(eventually_before(F)).
derive(always_after(F),release(false,F)) 
  :- subformula(always_after(F)).
derive(always_before(F),trigger(false,F)) 
  :- subformula(always_before(F)).
derive(release(L,R),until(R,and(R,or(L,final)))) 
  :- subformula(release(L,R)).
derive(trigger(L,R),since(R,and(R,or(L,initial)))) 
  :- subformula(trigger(L,R)).

subformula(F2) :- derive(F1,F2), subformula(F1).
true(F2,K) :- derive(F1,F2), true(F1,K).
true(F1,K) :- derive(F1,F2), true(F2,K).

not_atom(F) 
 :- subformula(F), 
    F=(true;false;initial;final;neg(_);next(_);prev(_);wprev(_);
    wnext(_);eventually(_);eventually_before(_);always(_);
    always_before(_);and(_,_);or(_,_);until(_,_);since(_,_);
    release(_,_);trigger(_,_);show(_)).

#show.
#show (F,K) : subformula(F), not not_atom(F), true(F,K), 
              not output(show,_).
#show (S,K) : output(show(S),B), conjunction(B,K).
#show (S,K) : time(K), output(show(S),B), literal_tuple(B,L), 
              not atom_tuple(_,L).

#defined literal_tuple/1.
#defined literal_tuple/2.
#defined rule/2.
#defined atom_tuple/2.
\end{lstlisting}
\captionof{lstlisting}{meta-telingo.lp - part 2}
\end{minipage}
\end{center}

\subsubsection{Meta Encoding}

Given an input temporal logic program (in it's gringo representation),
referred to as tap.lp we can obtain it's temporal stable models by
executing the command:

\begin{lstlisting}[language=bash,numbers=none]
clingo tap.lp --output=reify | clingo - meta-telingo.lp -c lambda=<n>
\end{lstlisting}

where \verb|<n>| is some natural number, and sets the trace length
$\lambda$ for the translation. We begin the description of
meta-telingo.lp encoding, which extends over Listing 1 and Listing 2.

We begin with Listing 1. In the first line, we introduce a helper
predicate \verb|time(K)|, which ranges over the time points of the trace we
are solving for. 

Lines 3-10 correspond to the formulas
$\{ \bigwedge_{k=0}^{\lambda-1} L_{b_1}^k \land \dots \land L_{b_n}
\rightarrow L_{h_1}^k \lor \dots \lor L_{h_m} \mid \tempruleshort \in
\Gamma \} \subset \tau(\Gamma)$. All of the rules are parameterized by
time point, and as such the ground instantiations of these rules will
form a conjunction over all time points, as in the corresponding set
of formulas in $\tau(\Gamma)$. The predicate \verb|hold(L,K)|
corresponds the atoms $L_{b_i}^k$ and $L_{h_j}$ (with a caveat
discussed in the next paragraph).

Lines 13-15 takes care of some technicalities required for the
implementation. The reification format of gringo represents literals
in the input program as integers \verb|L| in predicates
\verb|literal_tuple(B,L)|. This integer representation is then linked
to it's symbolic representation \verb|O| (a symbolic term in the
gringo language) via the predicate \verb|output(O,B)|. Our translation
will represent the atoms $L_\gamma^k \in \A_{\Gamma,\lambda}^*$ via
\verb|true(O,K)|, where \verb|O| is symbolic representation of
$\gamma$, and \verb|K| is a time point. Lines 13-14 make integer
representation \verb|hold(L,K)| equivalent to the symbolic
representation \verb|true(O,K)|. Line 15 handles an edge case. When,
in \verb|output(O,B)|, \verb|O| is a fact, the reified representation
generates no facts of the form \verb|literal_tuple(B,L)|. Line 15 thus
asserts the truth of such facts for all time points.

Lines 17-19 introduce the predicate \verb|subformula(F)|, which will
range over all formulas $\gamma \in subf^*(\Gamma)$. It starts on line
17 from the formulas occurring in the head and bodies of rules in
$\Gamma$, found in the first argument of \verb|output(O,_)|, and then
recursively collects all subformulas via the recursive rules on lines
18-19. Note that these rules do not collect all subformulas in
$subf^*(\Gamma)$; the derivations of derived operators will be
collected later in the encoding.

The rules in lines 21-61 do not require much explanation as they
express, more or less verbatim, the rules in $\pi^k(\gamma)$ as seen
in Figure \ref{fig:gamma-df-pi}. Note that the predicates
\verb|true(until(L,R,J),K)| and \verb|true(since(L,R,J),K)| correspond
to auxiliary atoms $L^k_{\varphi \until \psi,j}$ and
$L^k_{\varphi \since \psi,j}$, respectively.

Moving on to Listing 2, in lines 1-14, we introduce a new helper
predicate \verb|derive(F1,F2)|. The effect of the rules is the
following. If the formula \verb|F1| occurs as a subformula, and
\verb|F1| has a top-level derived operator, and the derivation of
\verb|F1| is \verb|F2|, then \verb|derive(F1,F2)| will hold. Line 16
adds the derivations of derived operators to the set subformulas,
completing the definition of \verb|subformula/1|. Lines 17-18 assert
that when \verb|derive(F1,F2)| holds, \verb|true(F1,K)| is equivalent
to \verb|true(F2,K)|, completing the translation from $\Gamma$ to
$\tau(\Gamma)$.

The lines 20-25 of Listing 2 tag any subformulas that are not atoms
via the predicate \verb|show/1|. This includes all of the symbols that
denote a temporal operator, as well as \verb|true| and
\verb|false|. We additionally mark symbols of the form \verb|show/1|
as not being atoms; the reason for this will become apparent in the
next paragraph.

Lines 27-32 are responsible for setting the output handling of
meta-telingo.lp, which is designed to mimic the output handling of
regular clingo. First, on line 27 we tell clingo to hide all atoms in
the output, and to show only terms that are explicitly marked to be
shown via a show statement. This is necessary, as by default, we wish
to only print the symbols corresponding to atoms. The statement on
lines 28-29 set up the default output handling. The statement shows a
term \verb|(F,K)| for all subformulas \verb|F| that are atomic
formulas and hold at time point \verb|K|; this behaviour is disabled
if the input program has a \verb|#show show.| statement, mimicking the
behaviour of clingo. 

Line 30 shows the term \verb|(S,K)|, if there is a show statement of
the form \verb|#show show(S): Body|, where \verb|Body| holds at time
point \verb|K|, again mimicking the behavior of show statements in
clingo. Lines 32-33 take care of showing \verb|(S,K)| at all time
points \verb|K|, if there is a show statement of the form
\verb|#show show(S).|, where the show statement has no body. The
reified representation of such statements can be distinguished from
statements with a body by the fact that there is no fact of the form
show \verb|atom_tuple(_,L)|, where \verb|L| is the integer
representation of \verb|S|.

Finally, lines 34-37 suppress info messages from clingo, when some
predicate in the reified representation over which there are no facts
produced by the reification of the input program. This can occur
during normal operation of the system, and the info messages would not
be particularly informative to the user, so we suppress them. This
concludes the meta-encoding.

\subsubsection{Issues Surrounding Grounding}

One flaw of the meta-telingo system that must bears mentioning is
caused by gringo during the initial grounding and reification of the
input program. As a highly optimized grounder, gringo performs some
simplifications of the input program during grounding. Namely, if
during grounding it is determined that an atom cannot occur in any
answer set, then all rules with that atom occurring positively in
their bodies are discarded, and all negative occurrences of that atom
in the body of any rule are removed. Furthermore, if an atom is
determined during grounding to hold in all answer sets, and rules with
a negative occurrence of the atom in their body are discarded
\cite{gekakasc12a}.

These simplifications preserve the stable models of the input program,
and therefore do not cause any issues, when reification and
meta-interpretation is not involved. However, these simplifications
may not be equivalent according to the semantics implemented by our
meta-encoding, as gringo has no knowledge of these semantics when
performing the aforementioned simplifications. Indeed, this is the
case for meta-telingo as well. Take for example the temporal program
from Example \ref{exam:shoot-ground-symbolic}. Setting $\lambda=3$ the
expected single temporal answer set for this program would be the
trace
$\bm{T}=(\{ shoot \}, \{ not\_load \}, \{ shoot, forgetful, fail \}
\})$. However, during grounding all of the rules of the program would
be discarded as the atoms \verb|initially| and
\verb|since(unloaded,prev(shoot))| do not occur in any rule head, and
so we would get the empty trace as the single temporal answer set.

Atoms can be guarded from simplification during grounding by using
clingo's external statements \cite{PotasscoUserGuide19}, so adding the
following additional external statements statement resolve this issue
in this specific case.

\begin{lstlisting}[language=clingo,numbers=none]
#external initial.
#external prev(since(not_load,shoot)).
\end{lstlisting}

Having to reason through gringo's grounding procedure and the
simplifications performed, and manually adding external statements is
hardly a good user experience for our system. Protecting rules from
simplification becomes even trickier if we allow our input program to
contain variables. Additionally, once variables are allowed, we must
face some challenges in the semantics of programs. 

\begin{example}\label{exam:shoot-nonground-unsafe}
  Take the following program, a modification of Example
  \ref{exam:shoot-ground-symbolic} that includes variables.
\begin{lstlisting}[language=clingo,numbers=none]
shoot(potato_gun) :- initial.
next(shoot(potato_gun)) :- initial.
and(eventually_after(fail(X)),forgetful(Y)) 
  :- shoot(X), prev(since(not_load(Y,X),shoot(X))).
\end{lstlisting}

Here, we give the predicates \verb|shoot| and \verb|fail| an arity of
1, allowing the final rule to take effect over an arbitrarily large
range of potato guns. The predicate \verb|not_load| now has an arity
of 2, with the second argument giving the potato gun, and the first
argument the person who would be responsible to load it after
shooting. 

Let us ignore for now the simplification of gringo. Since we
have two consecutive shots of \verb|potato_gun| at time points 0 and
1, the body of the final rule is satisfied at time step 1, regardless
of the truth value of the left operand of \verb|since|. So, assuming
the domain we ground the program over it's Herbrand universe (as is
usually the case for logic programs), for any substitution of a
constant \verb|c| from the universe for the variable \verb|Y|, we
should theoretically derive \verb|forgetful(c)|. As the program only
contains one constant, namely \verb|potato_gun|, we see that we should
derive \verb|forgetful(potato_gun)|, which is clearly
undesirable. Additionally, if we introduce a new fact
\verb|happy(flower).|, just by virtue of introducing a new constant to
the Herbrand universe, we should also derive \verb|forgetful(flower)|,
even though the predicate \verb|happy| is completely disconnected from
the rest of the program. This shows that the final rule violates the
important property of \textit{domain independence} that is guaranteed
in safe (regular) logic programs, hinting at the fact that the rule
should be considered \textit{unsafe}.
\end{example}

To be able to formally tackle the issues of safety and protecting
rules from simplification during grounding of the input temporal
program, will formally discuss the semantics of first order temporal
logic programs with variables in the following section. We will then
present a system for applying metaprogramming to non-ground ASP
programs to resolve the issues surrounding grounding in the
meta-telingo system.
